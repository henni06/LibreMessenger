apply plugin: 'java-library'
sourceCompatibility = 1.8
targetCompatibility = 1.8

apply plugin: 'net.ltgt.apt'
apply plugin: 'idea'
apply plugin: 'witness'
apply from: 'witness.gradle'

configurations {
	tor
}

dependencies {
	implementation project(path: ':bramble-core', configuration: 'default')
	implementation fileTree(dir: 'libs', include: '*.jar')
	implementation 'net.java.dev.jna:jna:4.4.0'
	implementation 'net.java.dev.jna:jna-platform:4.4.0'
	tor 'org.briarproject:tor-linux:0.2.9.16@zip'

	apt 'com.google.dagger:dagger-compiler:2.0.2'

	testImplementation project(path: ':bramble-api', configuration: 'testOutput')
	testImplementation project(path: ':bramble-core', configuration: 'testOutput')
	testImplementation 'junit:junit:4.12'
	testImplementation "org.jmock:jmock:2.8.2"
	testImplementation "org.jmock:jmock-junit4:2.8.2"
	testImplementation "org.jmock:jmock-legacy:2.8.2"
	testImplementation "org.hamcrest:hamcrest-library:1.3"
	testImplementation "org.hamcrest:hamcrest-core:1.3"
}

project.afterEvaluate {
	copy {
		from configurations.tor.collect { zipTree(it) }
		into 'src/main/resources'
	}
}

tasks.withType(Test) {
	// Use entropy-gathering device specified on command line, if any
	systemProperty 'java.security.egd', System.getProperty('java.security.egd')
	systemProperty 'java.library.path', 'libs'
}
