apply plugin: 'java'
sourceCompatibility = 1.8
targetCompatibility = 1.8

apply plugin: 'net.ltgt.apt'
apply plugin: 'idea'
apply plugin: 'witness'

dependencies {
	compile project(path: ':bramble-api', configuration: 'default')
	compile 'com.madgag.spongycastle:core:1.58.0.0'
	compile 'com.h2database:h2:1.4.192' // This is the last version that supports Java 1.6
	compile 'org.bitlet:weupnp:0.1.4'

	apt 'com.google.dagger:dagger-compiler:2.0.2'

	testCompile project(path: ':bramble-api', configuration: 'testOutput')

	testApt 'com.google.dagger:dagger-compiler:2.0.2'
}

dependencyVerification {
	verify = [
			'com.madgag.spongycastle:core:199617dd5698c5a9312b898c0a4cec7ce9dd8649d07f65d91629f58229d72728',
			'com.h2database:h2:225b22e9857235c46c93861410b60b8c81c10dc8985f4faf188985ba5445126c',
			'org.bitlet:weupnp:88df7e6504929d00bdb832863761385c68ab92af945b04f0770b126270a444fb',
	]
}

// needed to make test output available to bramble-j2se
configurations {
	testOutput.extendsFrom(testCompile)
}
task jarTest(type: Jar, dependsOn: testClasses) {
	from sourceSets.test.output
	classifier = 'test'
}
artifacts {
	testOutput jarTest
}

// If a Java 6 JRE is available, check we're not using any Java 7 or 8 APIs
tasks.withType(JavaCompile) {
	doFirst {
		def home = System.env.JAVA_6_HOME;
		if (home != null && !home.isEmpty()) {
			options.bootstrapClasspath = files("${home}/jre/lib/rt.jar", "${home}/jre/lib/jsse.jar")
		}
	}
}
