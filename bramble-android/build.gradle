import de.undercouch.gradle.tasks.download.Download
import de.undercouch.gradle.tasks.download.Verify

apply plugin: 'com.android.library'
apply plugin: 'witness'
apply plugin: 'de.undercouch.download'

android {
	compileSdkVersion 23
	buildToolsVersion "23.0.3"

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 22
		versionCode 14
		versionName "0.14"
		consumerProguardFiles 'proguard-rules.txt'
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}
}

dependencies {
	compile project(':bramble-core')
	compile fileTree(dir: 'libs', include: '*.jar')
	provided 'javax.annotation:jsr250-api:1.0'
}

def torBinaryDir = 'src/main/res/raw'

task downloadTorGeoIp(type: Download) {
	src 'https://briarproject.org/build/geoip-2017-05-02.zip'
	dest "$torBinaryDir/geoip.zip"
	onlyIfNewer true
}

task downloadTorBinaryArm(type: Download) {
	src 'https://briarproject.org/build/tor-0.2.9.11-arm.zip'
	dest "$torBinaryDir/tor_arm.zip"
	onlyIfNewer true
}

task downloadTorBinaryArmPie(type: Download) {
	src 'https://briarproject.org/build/tor-0.2.9.11-arm-pie.zip'
	dest "$torBinaryDir/tor_arm_pie.zip"
	onlyIfNewer true
}

task downloadTorBinaryX86(type: Download) {
	src 'https://briarproject.org/build/tor-0.2.9.11-x86.zip'
	dest "$torBinaryDir/tor_x86.zip"
	onlyIfNewer true
}

task downloadTorBinaryX86Pie(type: Download) {
	src 'https://briarproject.org/build/tor-0.2.9.11-x86-pie.zip'
	dest "$torBinaryDir/tor_x86_pie.zip"
	onlyIfNewer true
}

task verifyTorGeoIp(type: Verify, dependsOn: 'downloadTorGeoIp') {
	src "$torBinaryDir/geoip.zip"
	algorithm 'SHA-256'
	checksum '51f4d1272fb867e1f3b36b67a584e2a33c40b40f62305457d799fd399cd77c9b'
}

task verifyTorBinaryArm(type: Verify, dependsOn: 'downloadTorBinaryArm') {
	src "$torBinaryDir/tor_arm.zip"
	algorithm 'SHA-256'
	checksum '1da6008663a8ad98b349e62acbbf42c379f65ec504fa467cb119c187cd5a4c6b'
}

task verifyTorBinaryArmPie(type: Verify, dependsOn: 'downloadTorBinaryArmPie') {
	src "$torBinaryDir/tor_arm_pie.zip"
	algorithm 'SHA-256'
	checksum 'eb061f880829e05f104690ac744848133f2dacef04759d425a2cff0df32c271e'
}

task verifyTorBinaryX86(type: Verify, dependsOn: 'downloadTorBinaryX86') {
	src "$torBinaryDir/tor_x86.zip"
	algorithm 'SHA-256'
	checksum 'f5308aff8303daca082f82227d02b51ddedba4ab1d1420739ada0427ae5dbb41'
}

task verifyTorBinaryX86Pie(type: Verify, dependsOn: 'downloadTorBinaryX86Pie') {
	src "$torBinaryDir/tor_x86_pie.zip"
	algorithm 'SHA-256'
	checksum '889a6c81ac73d05d35ed610ca5a913cee44d333e4ae1749c2a107f2f7dd8197b'
}

project.afterEvaluate {
	preBuild.dependsOn {
		[
				'verifyTorGeoIp',
				'verifyTorBinaryArm',
				'verifyTorBinaryArmPie',
				'verifyTorBinaryX86',
				'verifyTorBinaryX86Pie'
		]
	}
}
