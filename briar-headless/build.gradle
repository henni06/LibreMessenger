plugins {
	id 'java'
	id 'net.ltgt.apt'
	id 'idea'
	id "org.jetbrains.kotlin.jvm" version "1.2.61"
	id 'witness'
}
apply from: 'witness.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	implementation project(path: ':briar-core', configuration: 'default')
	implementation project(path: ':bramble-java', configuration: 'default')

	implementation 'io.javalin:javalin:1.7.0'
	implementation 'org.slf4j:slf4j-simple:1.7.25'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.6'

	apt 'com.google.dagger:dagger-compiler:2.0.2'

	testImplementation project(path: ':bramble-api', configuration: 'testOutput')
	testImplementation project(path: ':bramble-core', configuration: 'testOutput')

	def junitVersion = '5.2.0'
	testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
	testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
	testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
	testImplementation "io.mockk:mockk:1.8.6"
}

jar {
	manifest {
		attributes(
				'Main-Class': 'org.briarproject.briar.headless.Main'
		)
	}
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}
